plugins {
    id 'application'
    id "org.panteleyev.jpackageplugin" version "1.3.1"
    id 'OpenSceneBuilder.java-conventions'
}

String platformName;
String platformNameRaw = System.getProperty('os.name').toLowerCase();
if(platformNameRaw.contains("windows")) {
    platformName = "win"
} else if(platformNameRaw.contains("linux")){
    platformName = "linux"
} else if(platformNameRaw.contains("mac")){
    platformName = "mac"
}else {
    platformName = ""
}

def openJfxVersion = "17.0.2"

dependencies {
    implementation "org.openjfx:javafx-base:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-fxml:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-controls:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-graphics:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-web:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-media:${openJfxVersion}:${platformName}"
    implementation "org.openjfx:javafx-swing:${openJfxVersion}:${platformName}"

    implementation project(":scenebuilder-kit")
}

application {
    mainModule = 'scenebuilder.app'
    mainClass = 'com.oracle.javafx.scenebuilder.app.SceneBuilderApp'
}

task copyDependencies(type:Copy) {
    from(configurations.runtimeClasspath).into("$buildDir/jmods").exclude("javafx-*-${openJfxVersion}.jar")
}

task copyJar(type:Copy) {
    from(tasks.jar).into("$buildDir/jmods")
}

// TODO: 適切なバージョンがまだない
//var versionRaw = java.lang.Runtime.version().version();
//var version = "${versionRaw.get(0)}.${versionRaw.get(1)}.${versionRaw.get(2)}"
var version = "17.0.2";

tasks.jpackage {
    dependsOn("build", "copyDependencies", "copyJar")

    appName = "OpenSceneBuilder"
    appVersion = version
    vendor = "OpenSceneBuilder project"
    copyright = "Copyright (c) 2021, OpenSceneBuilder project and/or its affiliates."
    licenseFile = "$path/../../LICENCE"
    appDescription = "OpenSceneBuilder OpenJFX GUI builder"
    runtimeImage = System.getProperty("java.home")
    module = "scenebuilder.app/com.oracle.javafx.scenebuilder.app.SceneBuilderApp"
    modulePaths = ["$buildDir/jmods"]
    destination = "$buildDir/dist"
    javaOptions = [
        "-XX:+UseZGC",
        "-Xmx1024m",
        "-Xms1024m",
        "-Dfile.encoding=UTF-8"
    ]

    windows {
        winDirChooser = true
        winMenu = true
        winShortcut = true
        winMenuGroup = "OpenSceneBuilder"
        winUpgradeUuid = "08df4fd1-8003-45ed-8f2e-08b9c2714abe"
    }
}
