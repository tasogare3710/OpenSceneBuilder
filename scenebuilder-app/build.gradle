plugins {
    id 'application'
	id "org.panteleyev.jpackageplugin" version "1.3.1"
}

repositories {
    mavenCentral()
}

String platformName;
String platformNameRaw = System.getProperty('os.name').toLowerCase();
if(platformNameRaw.contains("windows")) {
    platformName = "win"
} else if(platformNameRaw.contains("linux")){
    platformName = "linux"
} else if(platformNameRaw.contains("mac")){
    platformName = "mac"
}else {
    platformName = ""
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'

    implementation "org.openjfx:javafx-base:16:${platformName}"
    implementation "org.openjfx:javafx-fxml:16:${platformName}"
    implementation "org.openjfx:javafx-controls:16:${platformName}"
    implementation "org.openjfx:javafx-graphics:16:${platformName}"
    implementation "org.openjfx:javafx-web:16:${platformName}"
    implementation "org.openjfx:javafx-media:16:${platformName}"
    implementation "org.openjfx:javafx-swing:16:${platformName}"

    implementation project(":scenebuilder-kit")
}

application {
    mainModule = 'scenebuilder.app'
    mainClass = 'com.oracle.javafx.scenebuilder.app.SceneBuilderApp'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.named('test') {
    useJUnitPlatform()
}

task copyDependencies(type:Copy) {
    from(configurations.runtimeClasspath).into("$buildDir/jmods")
}

task copyJar(type:Copy) {
    from(tasks.jar).into("$buildDir/jmods")
}

var versionRaw = java.lang.Runtime.version().version();
var version = "${versionRaw.get(0)}.${versionRaw.get(1)}.${versionRaw.get(2)}"

tasks.jpackage {
    dependsOn("build", "copyDependencies", "copyJar")

    appName = "OpenSceneBuilder"
    // 適切なバージョンがまだない
    appVersion = version
    vendor = "OpenSceneBuilder project"
    copyright = "Copyright (c) 2021, OpenSceneBuilder project and/or its affiliates."
    licenseFile = "$path/../../LICENCE"
    appDescription = "OpenSceneBuilder OpenJFX GUI builder"
    runtimeImage = System.getProperty("java.home")
    module = "scenebuilder.app/com.oracle.javafx.scenebuilder.app.SceneBuilderApp"
    modulePaths = ["$buildDir/jmods"]
    destination = "$buildDir/dist"
    javaOptions = ["-Dfile.encoding=UTF-8"]

    windows {
        winDirChooser = true
        winMenu = true
        winShortcut = true
        winMenuGroup = "OpenSceneBuilder"
        winUpgradeUuid = "08df4fd1-8003-45ed-8f2e-08b9c2714abe"
    }
}
